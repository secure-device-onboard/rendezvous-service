// Copyright 2019 Intel Corporation
// SPDX-License-Identifier: Apache 2.0

repositories {
    mavenCentral()
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'org.springframework.boot'
apply from: "$rootDir/config/checkstyle.gradle"

version = System.getProperty('version') ?: '1.9'

jar{
     archivesBaseName = 'rendezvous-service'
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.2.6.RELEASE")
    }
}

compileJava {
    options.compilerArgs << "-Werror"
}

processResources {
    filesMatching('application.yml') {
        expand(project.properties)
    }
}

configurations {
    all {
        exclude module: "spring-boot-starter-tomcat"
        exclude module: "xml-apis"
        exclude group: "org.eclipse.jetty.websocket"
        exclude group: "commons-logging"
    }
}

dependencies {
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-rest', version:'2.2.6.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-jetty', version:'2.2.6.RELEASE'
    compile group: 'org.springframework', name: 'spring-aspects', version:'5.2.5.RELEASE'
    compile group: 'org.bouncycastle', name: 'bcprov-jdk15on', version:'1.64'
    compile group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version:'1.64'
    compile group: 'io.jsonwebtoken', name: 'jjwt', version:'0.9.1'
    compile group: 'redis.clients', name: 'jedis', version:'3.2.0'
    compile group: 'com.google.guava', name: 'guava', version:'28.2-jre'
    compile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
    compile group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '6.3'

    //Protecode jackson-databind, manually jackson updated
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.10.3'

    compileOnly group: 'org.projectlombok', name: 'lombok', version:'1.18.12'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version:'1.18.12'

    testCompile group: 'org.testng', name: 'testng', version:'7.1.0'
    testCompile group: 'org.powermock', name: 'powermock-api-mockito2', version:'2.0.7'
    testCompile group: 'org.powermock', name: 'powermock-module-testng', version:'2.0.7'
}

// Unit tests configuration
test {
    useTestNG()

    testLogging {
        exceptionFormat 'full'
        events 'FAILED'
    }
    afterTest { test, result ->
        if(result.resultType.equals(TestResult.ResultType.SUCCESS)) {
            float testExecutionTime = (result.endTime - result.startTime) / 1000.0 // in secs
            logger.lifecycle(String.format("%s.%s [%.3f sec] PASSED",
                    test.className,
                    test.name,
                    testExecutionTime))
        }
    }
    afterSuite { suite, result ->
        if(!suite.parent) {
            float suiteExecutionTime = (result.endTime - result.startTime) / 1000.0 // in secs
            logger.quiet("")
            logger.quiet(String.format("TEST SUITE TIME: %.3f sec", suiteExecutionTime))
            if(result.failedTestCount == 0) {
                logger.quiet("")
                logger.quiet(String.format("%d tests completed, %d failed, %d skipped",
                        result.successfulTestCount,
                        result.failedTestCount,
                        result.skippedTestCount))
                logger.quiet("")
            }
        }
    }
}

// Jacoco configuration
jacoco {
    toolVersion = "0.8.5"
}

task jacocoReportRendezvous(type: JacocoReport) {
    additionalSourceDirs = files(sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(sourceSets.main.allSource.srcDirs)
    classDirectories = files(sourceSets.main.output)
    executionData = files(jacocoTestReport.executionData)
    reports {
        html.enabled = true
        html.setDestination(file("${project.rootDir}/build/jacocoReport"))
        xml.enabled = true
        xml.setDestination(file("${project.rootDir}/build/jacocoReport/CoverageJacoco.xml"))
        csv.enabled = true
        csv.setDestination(file("${project.rootDir}/build/jacocoReport/CoverageJacoco.csv"))
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['org/sdo/rendezvous/development/**'])
        })
    }
}

task copyDockerDependencies(type: Copy){
    mkdir "${project.rootDir}/demo/certs"

    from ("${project.rootDir}"){
        include "certs/*.jks"
    }
    into "${project.rootDir}/demo/certs"

    from "${project.rootDir}/build/libs"
    into "${project.rootDir}/demo"
}

task cleanDockerFiles(type: Delete) {

    delete "${project.rootDir}/demo/certs"

    delete fileTree("${project.rootDir}/demo/") {
        include '**/*.jar'
    }
}

clean.dependsOn cleanDockerFiles
build.dependsOn copyDockerDependencies
